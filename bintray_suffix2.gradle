apply plugin: 'com.jfrog.bintray'

afterEvaluate { project ->
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }

    task javadoc(type: Javadoc) {
        failOnError false
        source = android.sourceSets.main.java.srcDirs
        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
            linksOffline "http://d.android.com/reference", System.getenv("ANDROID_HOME") + "/docs/reference"
        }
        classpath += project.android.libraryVariants.toList().first().javaCompile.classpath
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }
}

version = VERSION_NAME

// Bintray
// local.properties for bintray userName,apiKey,password
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = ARTIFACT_NAME + SUFFIX_2
        desc = ARTIFACT_DESCRIPTION
        websiteUrl = SITE_URL
        issueTrackerUrl = ISSUE_URL
        vcsUrl = SCM_URL
        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true

        version {
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}
